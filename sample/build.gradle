plugins {
    id "me.tatarka.retrolambda" version "3.2.5"
}

apply plugin: 'com.android.application'

android {
    signingConfigs {
        debug {
            keyAlias 'debugKey'
            keyPassword 'tester'
            storeFile file('../debug.jks')
            storePassword 'tester'
        }
    }
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    testBuildType "development"
    defaultConfig {
        applicationId "com.beloo.chipslayoutmanager.sample"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName rootProject.ext.libraryVersion

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        all {
            buildConfigField("boolean", "isReportCrashes", "true")
        }
        development {
            buildConfigField("boolean", "isReportCrashes", "false")
            debuggable true
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

retrolambda {
    javaVersion JavaVersion.VERSION_1_7
}

def supportVersion = "25+";

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //modules
    implementation project(':ChipsLayoutManager')
    //support libraries
    implementation "com.android.support:appcompat-v7:$supportVersion"
    implementation "com.android.support:design:$supportVersion"
    implementation "com.android.support:recyclerview-v7:$supportVersion"
    implementation "com.android.support:cardview-v7:$supportVersion"
    implementation "com.android.support:support-v4:$supportVersion"
    //3rd party libraries
    implementation('com.mikepenz:materialdrawer:5.8.0@aar') {
        transitive = true
    }
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    //butterKnife
    implementation 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'
    //crash tracking tool
    implementation 'com.google.firebase:firebase-crash:10.0.1'
    testCompile 'junit:junit:4.12'
    //android tests
    androidTestImplementation "com.android.support:support-annotations:$supportVersion"
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestImplementation 'com.android.support.test:runner:0.5'
    androidTestImplementation 'org.mockito:mockito-core:1.+'
    androidTestImplementation 'com.google.dexmaker:dexmaker:1.2'
    androidTestImplementation 'com.google.dexmaker:dexmaker-mockito:1.2'
    androidTestImplementation('com.android.support.test.espresso:espresso-contrib:2.2.2') {
        //this library has conflicts with app dependencies
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude module: 'recyclerview-v7'
    }
}


apply plugin: 'com.google.gms.google-services'